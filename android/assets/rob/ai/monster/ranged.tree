import evadeTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.actions.EvadeTask"
import hPAboveTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.conditions.HPAboveTask"
import inRangeTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.conditions.InRangeTask"
import isAliveTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.conditions.IsAliveTask"
import shootTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.actions.ShootTask"
import pursueTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.actions.PursueTask"
import wanderTask:"io.piotrjastrzebski.ecsclones.restrainingofbob.tasks.actions.WanderTask"

root
  selector
    # check if target is in range and alive
    sequence
      isAliveTask target:"player"
      inRangeTask target:"player" dst:7.0
      # run away or attack
      selector
        # evade target if low on hp and target is in range
        parallel policy:"Sequence"
          sequence
            invert
              hPAboveTask percent:30.0 absolute:0.0
            isAliveTask target:"player"
            inRangeTask target:"player" dst:10.0
          evadeTask target:"player"
        # pursue target if high hp and try to attack when close enough
        parallel policy:"Sequence"
          sequence
            hPAboveTask percent:30.0 absolute:0.0
            isAliveTask target:"player"
            inRangeTask target:"player" dst:10.0
          pursueTask target:"player"
          # we dont want to stop if we are not in attack range
          alwaysSucceed
            sequence
              # attack range
              inRangeTask target:"player" dst:7.0
              shootTask target:"player"
              # attack delay in seconds
              wait seconds:"constant,1"
    parallel policy:"Sequence"
      # stop wandering when player is close and alive
      invert
        sequence
          inRangeTask target:"player" dst:7.0
          isAliveTask target:"player"
      wanderTask
